use std::str::FromStr;
use crate::helpers::remove_first_last;
use crate::lexer::Token;
use crate::parser::Line;
use crate::error::ParseError;

grammar<'input>(input: &'input str);

pub Lines: Vec<Line<'input>> = <Line*>;

pub Line: Line<'input> = {
    Define => <>,
    BlockBegin => <>,
    BlockEnd => <>,
    Assignment => <>,
    "EOL" => Line::EndOfLine,
    <comment:"Comment"> => Line::Comment(comment),
}

BlockBegin: Line<'input> = {
    "BlockKeyword" "=" <name:"ValueString"> <comment:"Comment"> "EOL" "{" => Line::BlockBegin(name, Some(comment)),
    "BlockKeyword" "=" <name:"ValueString"> "EOL" "{" => Line::BlockBegin(name, None),
}

BlockEnd: Line<'input> = {
    "}" <comment:"Comment"> "EOL" => Line::BlockEnd(Some(comment)),
    "}" "EOL" => Line::BlockEnd(None),
}

Define: Line<'input> = {
    <d:"Define"> <name:"Key"> "=" <value:"ValueString"> <comment:"Comment"> "EOL" => Line::Define(name, value, Some(comment)),
    <d:"Define"> <name:"Key"> "=" <value:"ValueString"> "EOL" => Line::Define(name, value, None),
}

// TODO: I'm not sure we should be parsing variables like this here...
// It might be easier to treat everything as a string, and handle variables
// later. Technically, variables could be anything. E.G.
// export TEST="TEST=1234"
Assignment: Line<'input> = {
    <key:"Key"> "=" <value:"ValueString"> <comment:"Comment"> "EOL" => Line::Assignment(key,value, Some(comment)),
    <key:"Key"> "=" <value:"ValueString"> "EOL" => Line::Assignment(key,value, None),
    <key:"Variable"> "=" <value:"ValueString"> <comment:"Comment"> "EOL" => Line::Assignment(key,value, Some(comment)),
    <key:"Variable"> "=" <value:"ValueString"> "EOL" => Line::Assignment(key,value, None),
}

extern {  
    type Location = usize;
    type Error = ParseError;
    
    enum Token<'input> {
        "(" => Token::ParenOpen, 
        ")" => Token::ParenClose, 
        "=" => Token::AssignOp,
        "Comment" => Token::Comment(<&'input str>),
        "Quote" => Token::Quote(<&'input str>),
        "Define" => Token::DefineKeyword,
        "BlockKeyword" => Token::BlockKeyword(<&'input str>),
        "Key" => Token::Key(<&'input str>),
        "ValueString" => Token::ValueString(<&'input str>),
        "EOL" => Token::EOL,
        "Variable" => Token::Variable(<&'input str>),
        "{" => Token::CurlyOpen,
        "}" => Token::CurlyClose,
    }
}
