use std::str::FromStr;
use crate::helpers::remove_first_last;
use crate::lexer::{Location, Token};
use crate::parser::Line;
use crate::error::MoosParseError;
use lalrpop_util::ErrorRecovery;

//grammar<'input>(input: &'input str);
grammar<'err, 'input>(errors: &'err mut Vec<ErrorRecovery<Location, Token<'input>, MoosParseError>>, input: &'input str);

pub Lines: Vec<Line<'input>> = <Line*>;

pub Line: Line<'input> = {
    Define => <>,
    BlockBegin => <>,
    BlockEnd => <>,
    Assignment => <>,
    Integer => <>,
    "EOL" => Line::EndOfLine,
    <comment:"Comment"> => Line::Comment(comment),
    ! => { errors.push(<>); Line::Error },
}

BlockBegin: Line<'input> = {
    "BlockKeyword" "=" <name:"ValueString"> <comment:"Comment"?> <eol:"EOL"?> <l:@L> "{" <r:@R> =>? match eol {
        //None => Err(lalrpop_util::ParseError::User {
        //    error: MoosParseError::new_missing_new_line(l, r)
        //}),
        None => {
            let e = lalrpop_util::ErrorRecovery { 
                error: lalrpop_util::ParseError::User {
                    error: MoosParseError::new_missing_new_line(l, r)
                },
                dropped_tokens: vec![]
            };
            errors.push(e);
            Ok(Line::Error)
        }
        Some(_) => Ok(Line::BlockBegin(name, comment)),
    },
}

BlockEnd: Line<'input> = {
    "}" <comment:"Comment"?> "EOL" => Line::BlockEnd(comment),
}

Define: Line<'input> = {
    <d:"Define"> <name:"Key"> "=" <value:Value> <comment:"Comment"?> "EOL" => Line::Define(name, value, comment),
}

// TODO: I'm not sure we should be parsing variables like this here...
// It might be easier to treat everything as a string, and handle variables
// later. Technically, variables could be anything. E.G.
// export TEST="TEST=1234"
Assignment: Line<'input> = {
    <key:"Key"> "=" <value:Value> <comment:"Comment"?> "EOL" => Line::Assignment(key,value, comment),
    <key:"Variable"> "=" <value:Value> <comment:"Comment"?> "EOL" => Line::Assignment(key,value, comment),
}

// TODO: The original MOOS parser treated everything after the equal sign as
// the value. It also allowed for anything before the equal to be the key..
// Should we match the original implementation or write something that
// applies stricter rules. E.G. keys must be an identifier: [a-zA-Z0-9_-.]+
Value: crate::parser::Value<'input> = {
    <value:"bool"> => crate::parser::Value::Boolean(value),
    <value:"int"> => crate::parser::Value::Integer(value),
    <value:"float"> => crate::parser::Value::Float(value),
    <value:"ValueString"> => crate::parser::Value::String(value),
    <value:"Quote"> => crate::parser::Value::String(value),
}

Integer: Line<'input> = {
    <value:"int"> => Line::Integer(value),
}

extern {  
    type Location = crate::lexer::Location;
    type Error = MoosParseError;
    
    enum Token<'input> {
        "(" => Token::ParenOpen, 
        ")" => Token::ParenClose, 
        "=" => Token::AssignOp,
        "Comment" => Token::Comment(<&'input str>),
        "Quote" => Token::Quote(<&'input str>),
        "PartialQuote" => Token::PartialQuote(<&'input str>, <char>),
        "Define" => Token::DefineKeyword,
        "BlockKeyword" => Token::BlockKeyword(<&'input str>),
        "Key" => Token::Key(<&'input str>),
        "ValueString" => Token::ValueString(<&'input str>),
        "EOL" => Token::EOL,
        "Variable" => Token::Variable(<&'input str>),
        "PartialVariable" => Token::PartialVariable(<&'input str>, <char>),
        "{" => Token::CurlyOpen,
        "}" => Token::CurlyClose,
        "int" => Token::Integer(<i64>),
        "float" => Token::Float(<f64>),
        "bool" => Token::Boolean(<bool>),
    }
}
